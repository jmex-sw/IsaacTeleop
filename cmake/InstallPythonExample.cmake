# SPDX-FileCopyrightText: Copyright (c) 2025-2026 NVIDIA CORPORATION & AFFILIATES. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

# ==============================================================================
# InstallPythonExample.cmake
# ==============================================================================
# Macro to install a Python example directory with a generated pyproject.toml.
#
# Reads the source pyproject.toml and appends a [tool.uv] block only to the
# installed copy so that `uv run` works out of the box in the install tree. On the
# way it pins isaacteleop to the version this build produced (preserving extras
# like [cloudxr]) and drops any [tool.uv.sources], so the installed example
# resolves the wheel next to it. Generation runs at configure time; the
# CMAKE_CONFIGURE_DEPENDS below re-runs configure when the source pyproject
# changes so `cmake --build && cmake --install` (e.g. scripts/run_example.sh)
# picks up dependency edits.
#
# Usage:
#   install_python_example(DESTINATION examples/oxr/python)
#   install_python_example(DESTINATION examples/teleop_ros2/python
#       EXTRA_UV_EXTRA_BUILD_DEPS "nlopt = [\"numpy\"]")
# ==============================================================================

macro(install_python_example)
    cmake_parse_arguments(_IPE "" "DESTINATION;EXTRA_UV_EXTRA_BUILD_DEPS" "" ${ARGN})
    if(NOT _IPE_DESTINATION)
        message(FATAL_ERROR "install_python_example: DESTINATION is required")
    endif()

    # Installed example is intended to run against a locally built wheel, so
    # required-environments must list only the current build arch.
    if(CMAKE_HOST_SYSTEM_PROCESSOR MATCHES "^(aarch64|arm64)$")
        set(_IPE_PLATFORM_MACHINE "aarch64")
    else()
        set(_IPE_PLATFORM_MACHINE "x86_64")
    endif()

    # Reconfigure when the source pyproject changes (file(READ) is configure-time).
    set_property(DIRECTORY APPEND PROPERTY CMAKE_CONFIGURE_DEPENDS
        "${CMAKE_CURRENT_SOURCE_DIR}/python/pyproject.toml")

    # Read the bare pyproject.toml and append uv configuration for the
    # installed environment.
    file(READ "${CMAKE_CURRENT_SOURCE_DIR}/python/pyproject.toml" _PYPROJECT_BASE)

    # A source-tree [tool.uv.sources] path is relative to the pyproject, so from
    # the installed copy it points at the install prefix, which is not a Python
    # project. Matched at line start, so prose naming the header is left alone.
    string(REGEX REPLACE "\n\\[tool\\.uv\\.sources\\][^[]*" "\n" _PYPROJECT_BASE "${_PYPROJECT_BASE}")

    # Pin to the wheel this build produced. Unversioned, uv would prefer the
    # newest release on an index whenever the local wheel is a pre-release --
    # which every CI build is (see IsaacTeleopVersion.cmake).
    # TODO(#880): no test covers the generated pyproject.
    string(REGEX REPLACE "\"(isaacteleop(\\[[^]]*\\])?)\""
        "\"\\1==${ISAAC_TELEOP_PYPROJECT_VERSION}\"" _PYPROJECT_BASE "${_PYPROJECT_BASE}")
    set(_TOOL_UV_BLOCK "[tool.uv]
find-links = [\"../../../wheels\"]
python-preference = \"only-managed\"
environments = [\"python_version == '${ISAAC_TELEOP_PYTHON_VERSION}'\"]
required-environments = [\"sys_platform == 'linux' and platform_machine == '${_IPE_PLATFORM_MACHINE}'\"]
")
    if(_IPE_EXTRA_UV_EXTRA_BUILD_DEPS)
        string(APPEND _TOOL_UV_BLOCK "
[tool.uv.extra-build-dependencies]
${_IPE_EXTRA_UV_EXTRA_BUILD_DEPS}
")
    endif()
    file(WRITE "${CMAKE_CURRENT_BINARY_DIR}/pyproject.toml"
        "${_PYPROJECT_BASE}\n${_TOOL_UV_BLOCK}")

    # Install generated pyproject.toml (with [tool.uv] appended)
    install(FILES "${CMAKE_CURRENT_BINARY_DIR}/pyproject.toml"
        DESTINATION ${_IPE_DESTINATION}
    )

    # Install Python example sources
    install(DIRECTORY python/
        DESTINATION ${_IPE_DESTINATION}
        FILES_MATCHING
            PATTERN "*.py"
            PATTERN "*.md"
        PATTERN ".venv" EXCLUDE
        PATTERN "__pycache__" EXCLUDE
        PATTERN "*.pyc" EXCLUDE
    )
endmacro()
